; lookup tables of RNG values for different framerules, each offset is another 2100 frames
resume_0: .byte $28, $34, $0c, $55, $f8, $fa, $97, $c0, $15, $05, $ed, $7e, $95, $ae, $09, $b4, $e0, $9a, $50, $f6, $ac, $99, $91, $8d, $9c, $4f, $fe, $9a, $49, $3a, $47, $4a, $ee, $96, $64, $82, $b9, $65, $d3, $e7, $38, $03, $97, $27, $e0, $32, $35, $fa, $43, $38, $b1, $75, $a4, $c1, $e0, $58, $49, $28, $7b, $9c, $6e, $cf, $df, $61, $ae, $95, $4a, $b7, $e7, $25, $12, $d0, $d3, $8a, $d2, $19, $95, $9a, $92, $ef, $72, $4e, $94, $72, $de, $0c, $d0, $ca, $46, $39, $ca, $ea, $81, $6a, $22, $35, $b1, $0e, $af
resume_1: .byte $02, $86, $c4, $17, $a1, $53, $48, $01, $65, $c5, $06, $42, $a4, $db, $67, $d7, $87, $5b, $36, $d7, $3d, $02, $ec, $b8, $c5, $99, $f7, $97, $bb, $1e, $65, $48, $8f, $a7, $44, $fb, $6e, $fb, $44, $3a, $3c, $41, $8e, $33, $95, $b0, $53, $97, $09, $fc, $e6, $69, $5d, $ca, $f7, $90, $2d, $d6, $5f, $51, $22, $c0, $78, $ef, $f7, $7c, $28, $dc, $d4, $f3, $54, $4d, $1a, $99, $d5, $ed, $9a, $2b, $fd, $cc, $3c, $60, $bf, $76, $bd, $24, $81, $97, $50, $8d, $a7, $f9, $34, $7c, $ae, $a5, $0c, $40, $52
resume_2: .byte $52, $ef, $dd, $bd, $50, $a7, $66, $81, $4f, $ce, $dc, $be, $8f, $86, $75, $be, $46, $6f, $96, $3a, $65, $30, $cf, $a3, $fc, $06, $0a, $a2, $28, $6a, $eb, $dc, $52, $8a, $8c, $fe, $1c, $30, $e2, $f4, $4c, $47, $a1, $7d, $54, $d5, $39, $62, $8f, $8d, $85, $83, $15, $49, $36, $21, $bf, $87, $a9, $69, $fe, $5f, $c6, $2c, $aa, $56, $bc, $b3, $1b, $b8, $70, $ed, $bc, $8c, $70, $de, $b1, $1f, $d8, $13, $d8, $fc, $96, $92, $00, $3c, $20, $02, $dc, $fe, $32, $2c, $36, $a8, $eb, $ce, $6e, $5c, $0c
resume_3: .byte $56, $e2, $54, $92, $12, $00, $f6, $82, $85, $45, $d1, $3b, $c6, $31, $bb, $11, $48, $d9, $fb, $94, $1f, $34, $16, $d2, $77, $34, $e4, $8d, $5e, $56, $20, $4d, $4c, $c5, $05, $09, $c0, $c6, $6b, $81, $34, $c5, $bc, $1b, $7e, $b4, $9f, $4c, $9c, $74, $48, $50, $af, $dd, $d8, $01, $e4, $2a, $16, $cb, $bb, $df, $37, $f2, $45, $ae, $ed, $0a, $b3, $5f, $d8, $76, $89, $bf, $da, $05, $84, $49, $23, $8b, $a1, $3d, $e9, $7f, $7a, $74, $22, $2c, $7d, $e5, $7c, $de, $5e, $51, $b6, $85, $76, $dc, $a8
resume_4: .byte $f2, $3d, $ee, $e9, $b2, $4e, $3b, $81, $1a, $d9, $68, $47, $d9, $3d, $50, $6d, $c4, $06, $d6, $e1, $d5, $54, $88, $95, $8f, $38, $f1, $c8, $0e, $82, $f6, $f5, $e8, $d0, $1d, $f5, $f9, $a7, $af, $68, $ac, $4a, $ff, $e1, $d6, $1f, $ec, $88, $83, $6e, $42, $56, $84, $4e, $b5, $43, $9b, $24, $44, $18, $46, $60, $bb, $ab, $11, $03, $94, $6c, $84, $2f, $39, $ac, $f0, $a6, $3b, $b9, $e7, $77, $93, $ac, $10, $c5, $c4, $5b, $7a, $0c, $62, $28, $c5, $18, $18, $87, $32, $01, $61, $18, $aa, $65, $b1
resume_5: .byte $5f, $f9, $47, $cc, $97, $4e, $d7, $84, $10, $52, $ca, $31, $54, $5f, $26, $4f, $55, $b4, $21, $c8, $ea, $3c, $a5, $30, $60, $50, $38, $d3, $b2, $2f, $b7, $6e, $71, $5b, $17, $e6, $78, $2a, $78, $6a, $c5, $c0, $86, $d6, $2b, $77, $d3, $11, $ba, $86, $d2, $f6, $db, $f4, $04, $41, $52, $70, $68, $8e, $30, $de, $d4, $4e, $9b, $5f, $4f, $78, $e3, $91, $89, $41, $e3, $d9, $8f, $b2, $ee, $e5, $d4, $bb, $52, $be, $17, $a5, $8e, $e4, $26, $70, $3e, $d2, $e0, $3a, $8e, $a3, $0d, $12, $47, $dd, $e0
resume_6: .byte $bb, $82, $9b, $1f, $f2, $d2, $a0, $87, $24, $e0, $1b, $bf, $e6, $25, $86, $95, $dd, $b9, $8d, $0b, $41, $94, $b4, $1a, $7e, $20, $da, $42, $af, $2b, $5a, $84, $a1, $fb, $2d, $0d, $8a, $64, $26, $ba, $9c, $55, $79, $15, $87, $49, $0a, $01, $bd, $5b, $57, $5b, $d2, $69, $6e, $c7, $64, $38, $e0, $bf, $bc, $1f, $a3, $18, $b8, $59, $67, $a0, $ea, $ce, $fa, $19, $02, $94, $f8, $c1, $21, $0a, $f3, $e2, $72, $35, $9f, $12, $7b, $fd, $e2, $20, $b4, $e3, $d1, $34, $eb, $a0, $cf, $22, $13, $16, $83

; ================================================================
;  Setup framerule RNG
; ----------------------------------------------------------------
RNGQuickResume:
    lda MathFrameruleDigitStart+3        ; get hundreds and thousands digits of bcd framerule value
    jsr MultiplyBy10                     ;
    adc MathFrameruleDigitStart+2        ;
    tax                                  ; store in X
    lda resume_0,x                       ; get rng value from lookup table for this offset
    sta PseudoRandomBitReg+0             ;
    lda resume_1,x                       ;
    sta PseudoRandomBitReg+1             ;
    lda resume_2,x                       ;
    sta PseudoRandomBitReg+2             ;
    lda resume_3,x                       ;
    sta PseudoRandomBitReg+3             ;
    lda resume_4,x                       ;
    sta PseudoRandomBitReg+4             ;
    lda resume_5,x                       ;
    sta PseudoRandomBitReg+5             ;
    lda resume_6,x                       ;
    sta PseudoRandomBitReg+6             ;
@FrameruleAdjust:
    lda MathFrameruleDigitStart + 1      ; get ones and tens digits of bcd framerule value
    jsr MultiplyBy10                     ;
    adc MathFrameruleDigitStart + 0      ;
    tay                                  ; store in Y
    beq @FrameAdjust                     ; if not set, we can skip ahead
    jsr FRStepRNGByY                     ; otherwise we advance the RNG based on the framerule value * 21
@FrameAdjust:
    ldy IncrementRNG                 	 ; did we hold A or B for a frame offset?
    beq :+                               ; branch ahead if not
    dey									 ; did we hold A for both quests patterns?
    beq @BothQuestsRNG					 ; if so, branch ahead to offset rng
    ldy #254							 ; otherwise set Y for 431 frames (254 + 177)
    jsr StepRNGByY						 ; and adjust rng for all stages patterns
    ldy #177							 ; hallo :D
    jsr StepRNGByY                       ;
    beq :+                               ; unconditional branch when done
@BothQuestsRNG:
    ldy #122                             ; set Y for 122 frames
    jsr StepRNGByY                       ; and adjust rng for both endings patterns
:   ldx SettablesPUP                     ; check if player has selected a powerup
    bne :+                               ; yes - skip ahead to adjust rng
    rts                                  ; no - we are done!
:   ldy @FramerulePowerupAdjust-1,x      ; check rng steps for current powerup value
    jsr StepRNGByY                       ; and adjust the rng for this value
    dex                                  ; decrement to next powerup value
    bne :-                               ; and loop until we reach small mario state
    rts                                  ; then we are done!
@FramerulePowerupAdjust:
    .byte 59                            ; fr pause from grabbing mushroom
    .byte 63                            ; fr pause from grabbing fireflower
    .byte 254 - 63                      ; fr pause from small fire (minus fr pause from grabbing fire)
    .byte 59                            ; fr pause from grabbing mushroom
    .byte 63                            ; fr pause from grabbing fireflower
; ================================================================

; ================================================================
;  Advance RNG by Y framerules
; ----------------------------------------------------------------
FRStepRNGByY:
    jsr FRStepRNG
    dey
    bne FRStepRNGByY
    rts
; ================================================================

; ================================================================
;  Advance RNG by Y frames
; ----------------------------------------------------------------
StepRNGByY:
    jsr SingleStepRNG
    dey
    bne StepRNGByY
    rts
; ================================================================

; ================================================================
;  Advance RNG by 21 frames
; ----------------------------------------------------------------
FRStepRNG:
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jmp SingleStepRNG
; ================================================================

; ================================================================
;  Advance RNG by a single game frame
; ----------------------------------------------------------------
SingleStepRNG:
    pha
    lda PseudoRandomBitReg         ; get first memory location of LSFR bytes
    and #%00000010                 ; mask out all but d1
    sta $00                        ; save here
    lda PseudoRandomBitReg+1       ; get second memory location
    and #%00000010                 ; mask out all but d1
    eor $00                        ; perform exclusive-OR on d1 from first and second bytes
    clc                            ; if neither or both are set, carry will be clear
    beq RotPRandomBit
    sec                            ; if one or the other is set, carry will be set
RotPRandomBit:
    ror PseudoRandomBitReg+0       ; rotate carry into d7, and rotate last bit into carry
    ror PseudoRandomBitReg+1       ; rotate carry into d7, and rotate last bit into carry
    ror PseudoRandomBitReg+2       ; rotate carry into d7, and rotate last bit into carry
    ror PseudoRandomBitReg+3       ; rotate carry into d7, and rotate last bit into carry
    ror PseudoRandomBitReg+4       ; rotate carry into d7, and rotate last bit into carry
    ror PseudoRandomBitReg+5       ; rotate carry into d7, and rotate last bit into carry
    ror PseudoRandomBitReg+6       ; rotate carry into d7, and rotate last bit into carry
    pla
    rts
; ================================================================
