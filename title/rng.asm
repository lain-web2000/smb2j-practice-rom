; lookup tables of RNG values for different framerules, each offset is another 2100 frames
resume_0: .byte $2e, $bf, $41, $6d, $1b, $0e, $4f, $c2, $ba, $4b, $e6, $b9, $78, $c2, $a0, $cc, $69, $77, $0b, $8f, $eb, $9f, $a4, $44, $d2, $e4, $e7, $22, $84, $18, $e9, $93, $7f, $7e, $a9, $8e, $0b, $68, $1e, $a8, $7c, $c0, $04, $72, $5f, $e3, $c0, $43, $59, $3d, $9a, $cf, $43, $9f, $f9, $e8, $3f, $53, $9b, $6f, $09, $8f, $56, $50, $b6, $11, $33, $d2, $9b, $31, $ee, $06, $fc, $20, $ad, $2f, $3a, $e7, $94, $bb, $37, $ef, $54, $e9, $19, $60, $53, $73, $b7, $af, $23, $e1, $dd, $ef, $d1, $db, $a5, $ce, $59
resume_1: .byte $f4, $b6, $a3, $d1, $51, $ba, $e3, $83, $b5, $97, $63, $80, $34, $51, $f9, $15, $4d, $eb, $8d, $98, $92, $52, $bd, $5c, $c1, $93, $e8, $70, $c3, $a0, $58, $2c, $79, $38, $d0, $f9, $4b, $aa, $62, $27, $e8, $89, $6c, $b4, $69, $ce, $19, $31, $99, $6b, $69, $ec, $63, $10, $ac, $f3, $ed, $5f, $a4, $eb, $21, $20, $32, $9a, $09, $87, $ed, $09, $f8, $3b, $c5, $7a, $df, $0c, $44, $8f, $48, $0a, $95, $f8, $55, $64, $04, $c0, $bd, $60, $e1, $91, $34, $8e, $f1, $8c, $6e, $84, $92, $f6, $be, $a5, $47
resume_2: .byte $a9, $c9, $20, $0a, $67, $a7, $7c, $06, $c0, $00, $af, $f3, $c4, $d5, $b8, $8d, $9f, $04, $9a, $87, $45, $6c, $f4, $d4, $64, $5a, $27, $34, $ca, $91, $8a, $0a, $87, $c4, $83, $e4, $5d, $7b, $5e, $77, $11, $08, $64, $51, $d7, $09, $99, $b7, $2a, $11, $5d, $73, $e5, $2e, $5f, $22, $92, $f9, $93, $34, $33, $3e, $9e, $3b, $65, $a4, $8a, $ad, $cf, $59, $18, $76, $26, $4c, $1e, $d0, $3c, $c4, $bc, $8f, $3b, $ba, $ac, $13, $8e, $a0, $46, $76, $5a, $d1, $b6, $4f, $d4, $5b, $31, $41, $f5, $38, $f5
resume_3: .byte $40, $a4, $66, $a8, $c5, $d2, $ba, $00, $ab, $2e, $68, $f2, $ad, $76, $4b, $a6, $04, $d2, $81, $b6, $61, $c8, $8f, $6d, $e6, $7c, $f7, $d4, $4d, $d0, $3b, $52, $74, $b5, $22, $17, $cb, $2f, $9a, $39, $c1, $1a, $bc, $39, $04, $95, $aa, $d4, $18, $c7, $8f, $ab, $22, $0e, $07, $c4, $49, $46, $da, $e2, $71, $7e, $fb, $0f, $77, $ab, $51, $be, $3e, $2f, $92, $82, $98, $54, $96, $cf, $ac, $d1, $97, $7e, $91, $73, $a5, $93, $f5, $61, $84, $54, $32, $cc, $55, $57, $09, $53, $15, $ad, $88, $72, $7a
resume_4: .byte $00, $37, $26, $bd, $0a, $9d, $43, $0c, $2a, $2e, $36, $15, $24, $dc, $3b, $bd, $3a, $db, $b4, $b9, $eb, $11, $66, $c5, $2f, $c8, $b8, $bd, $d9, $f3, $2f, $46, $7a, $3c, $24, $df, $70, $d9, $27, $d7, $e2, $0a, $75, $9b, $aa, $86, $99, $bb, $4c, $e4, $34, $4c, $e8, $52, $b9, $81, $6d, $b4, $fd, $8b, $17, $02, $c6, $79, $bd, $e2, $45, $e5, $a0, $9d, $a3, $6f, $d5, $cc, $ab, $6e, $d5, $58, $ee, $60, $e6, $07, $fc, $b4, $e8, $21, $09, $b8, $86, $6f, $39, $c9, $a1, $e5, $77, $2e, $63, $02, $90
resume_5: .byte $00, $7f, $ea, $ec, $80, $38, $37, $0c, $7c, $72, $e6, $f1, $7e, $31, $ad, $f0, $32, $7e, $b7, $d4, $28, $81, $78, $1e, $e3, $31, $57, $14, $42, $52, $59, $e2, $92, $56, $60, $f0, $e6, $86, $13, $a4, $61, $3e, $0d, $e8, $a3, $ad, $cc, $12, $7c, $6b, $2a, $1a, $ad, $4e, $b6, $08, $ff, $39, $48, $4e, $f5, $fe, $31, $67, $52, $b5, $e7, $98, $dd, $c2, $86, $6b, $e4, $65, $86, $f0, $8c, $fa, $c1, $9c, $c5, $e1, $b7, $93, $03, $e3, $01, $11, $e3, $f7, $93, $66, $b2, $42, $5d, $74, $73, $e6, $65
resume_6: .byte $00, $11, $a7, $97, $95, $02, $b1, $14, $28, $2e, $8b, $da, $36, $89, $da, $8b, $46, $c8, $de, $a7, $fe, $a2, $b4, $94, $bc, $a1, $27, $6e, $f0, $b4, $07, $6f, $67, $2e, $28, $4f, $07, $35, $5d, $0b, $a5, $2a, $e7, $df, $f6, $a0, $ff, $64, $e4, $a3, $42, $82, $7c, $ea, $c5, $0a, $24, $51, $b2, $58, $da, $fb, $bd, $95, $28, $70, $6c, $53, $9c, $f9, $c1, $b5, $4f, $fd, $d1, $2d, $27, $4b, $1c, $5d, $08, $ee, $4e, $fa, $d3, $a0, $13, $61, $ee, $28, $e0, $f4, $f1, $88, $b3, $28, $b5, $e3, $45

; ================================================================
;  Setup framerule RNG
; ----------------------------------------------------------------
RNGQuickResume:
    lda MathFrameruleDigitStart+3        ; get hundreds and thousands digits of bcd framerule value
    jsr MultiplyBy10                     ;
    adc MathFrameruleDigitStart+2        ;
    tax                                  ; store in X
    lda resume_0,x                       ; get rng value from lookup table for this offset
    sta PseudoRandomBitReg+0             ;
    lda resume_1,x                       ;
    sta PseudoRandomBitReg+1             ;
    lda resume_2,x                       ;
    sta PseudoRandomBitReg+2             ;
    lda resume_3,x                       ;
    sta PseudoRandomBitReg+3             ;
    lda resume_4,x                       ;
    sta PseudoRandomBitReg+4             ;
    lda resume_5,x                       ;
    sta PseudoRandomBitReg+5             ;
    lda resume_6,x                       ;
    sta PseudoRandomBitReg+6             ;
@FrameruleAdjust:
    lda MathFrameruleDigitStart + 1      ; get ones and tens digits of bcd framerule value
    jsr MultiplyBy10                     ;
    adc MathFrameruleDigitStart + 0      ;
    tay                                  ; store in Y
    beq @FrameAdjust                     ; if not set, we can skip ahead
    jsr FRStepRNGByY                     ; otherwise we advance the RNG based on the framerule value * 21
@FrameAdjust:
    lda IncrementRNG_122                 ; did we hold A for a 122 frame offset?
    beq :+                               ; branch ahead if not
    ldy #122                             ; otherwise set Y for 122 frames
    jsr StepRNGByY                       ; and adjust rng for both endings patterns
:   ldx SettablesPUP                     ; check if player has selected a powerup
    bne :+                               ; yes - skip ahead to adjust rng
    rts                                  ; no - we are done!
:   ldy @FramerulePowerupAdjust-1,x      ; check rng steps for current powerup value
    jsr StepRNGByY                       ; and adjust the rng for this value
    dex                                  ; decrement to next powerup value
    bne :-                               ; and loop until we reach small mario state
    rts                                  ; then we are done!
@FramerulePowerupAdjust:
    .byte 59                            ; fr pause from grabbing mushroom
    .byte 63                            ; fr pause from grabbing fireflower
    .byte 254 - 63                      ; fr pause from small fire (minus fr pause from grabbing fire)
    .byte 59                            ; fr pause from grabbing mushroom
    .byte 63                            ; fr pause from grabbing fireflower
; ================================================================

; ================================================================
;  Advance RNG by Y framerules
; ----------------------------------------------------------------
FRStepRNGByY:
    jsr FRStepRNG
    dey
    bne FRStepRNGByY
    rts
; ================================================================

; ================================================================
;  Advance RNG by Y frames
; ----------------------------------------------------------------
StepRNGByY:
    jsr SingleStepRNG
    dey
    bne StepRNGByY
    rts
; ================================================================

; ================================================================
;  Advance RNG by 21 frames
; ----------------------------------------------------------------
FRStepRNG:
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jsr SingleStepRNG
    jmp SingleStepRNG
; ================================================================

; ================================================================
;  Advance RNG by a single game frame
; ----------------------------------------------------------------
SingleStepRNG:
    pha
    lda PseudoRandomBitReg         ; get first memory location of LSFR bytes
    and #%00000010                 ; mask out all but d1
    sta $00                        ; save here
    lda PseudoRandomBitReg+1       ; get second memory location
    and #%00000010                 ; mask out all but d1
    eor $00                        ; perform exclusive-OR on d1 from first and second bytes
    clc                            ; if neither or both are set, carry will be clear
    beq RotPRandomBit
    sec                            ; if one or the other is set, carry will be set
RotPRandomBit:
    ror PseudoRandomBitReg+0       ; rotate carry into d7, and rotate last bit into carry
    ror PseudoRandomBitReg+1       ; rotate carry into d7, and rotate last bit into carry
    ror PseudoRandomBitReg+2       ; rotate carry into d7, and rotate last bit into carry
    ror PseudoRandomBitReg+3       ; rotate carry into d7, and rotate last bit into carry
    ror PseudoRandomBitReg+4       ; rotate carry into d7, and rotate last bit into carry
    ror PseudoRandomBitReg+5       ; rotate carry into d7, and rotate last bit into carry
    ror PseudoRandomBitReg+6       ; rotate carry into d7, and rotate last bit into carry
    pla
    rts
; ================================================================
